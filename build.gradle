buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.4.8.3"

group = "ivorius.reccomplex" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "RecurrentComplex"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}
task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each {
            if (it.name == '__plugin_repository__Gradle Central Plugin Repository') {
                println "Name: " + it.name + "; url: " + it.url
            } else {
                println "Name: " + it.displayName
            }
        }
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

repositories {
    mavenLocal()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2859'
    compile fg.deobf('ivorius.ivtoolkit:IvToolkit:1.3.3-1.12')
    shade fg.deobf('ivorius.mcopts:MCOpts:0.9.9.4')
}

jar {
    archiveClassifier = 'slim'

    manifest {
        attributes([
                "Specification-Title"     : "mcopts",
                "Specification-Vendor"    : "ivorius",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "ivorius",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// --- shadow
shadowJar {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    relocate 'ivorius.mcopts', "${project.group}..shadow.mcopts"
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}
// --- shadow end

// merge tool fix
configurations.runtimeClasspath {
    exclude group: 'net.minecraftforge', module: 'mergetool'
}

publish.dependsOn('reobfJar')

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
